plugins {
    id 'java'
    id 'application'
}

group 'mc'
version '0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation project('lib:models')
    implementation files('lib/Jeigen-onefat.jar')
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.6'
    implementation group: 'net.java.dev.jna', name: 'jna', version: '4.1.0'
    implementation group: 'info.picocli', name: 'picocli', version: '4.6.2'
    implementation group: 'org.roaringbitmap', name: 'RoaringBitmap', version: '0.8.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

task extractScriptTemplates {
    doLast {
        file("lib/models/config/template-unix.default.txt").text =
                startScripts.unixStartScriptGenerator.template.asString()
        file("lib/models/config/template-windows.default.txt").text =
                startScripts.windowsStartScriptGenerator.template.asString()
    }
}

startScripts {
    unixStartScriptGenerator.template = resources.text.fromFile("lib/models/config/template-unix.txt")
    windowsStartScriptGenerator.template = resources.text.fromFile("lib/models/config/template-windows.txt")
}

application {
    mainClass = 'stationary.Main'
}

distributions {
    main {
        contents {
            from("$rootProject.buildDir/lib/") {
                include "*.dll"
                include "*.so"
                into "lib"
            }
        }
    }
}
